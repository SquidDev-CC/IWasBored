buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'

group = "org.squiddev"
archivesBaseName = "IWasBored"
version = mc_version + "-" + mod_version

repositories {
	mavenCentral()

	maven {
		name = "chickenbones"
		url = "http://chickenbones.net/maven"
	}

	maven {
		name = "squiddev"
		url = "http://maven.bonzodandd.co.uk"
	}

	maven {
		name = "RX14 Proxy"
		url = "http://mvn.rx14.co.uk/repo/"
	}

	ivy {
		name = "ComputerCraft"
		artifactPattern "http://addons-origin.cursecdn.com/files/2244/605/[module][revision].[ext]"
	}
}

dependencies {
	compile "codechicken:CodeChickenCore:${project.mc_version}-1.0.4.29:dev"
	compile "codechicken:CodeChickenLib:${project.mc_version}-1.1.3.127:dev"

	compile group: 'ComputerCraft', name: 'ComputerCraft', version: "1.74", ext: 'jar'
	compile "org.squiddev:CCTweaks:${project.mc_version}-0.2.3.2:dev"
	compile "com.azanor.baubles:Baubles:${project.mc_version}-1.0.1.10:deobf"
}

minecraft {
	version = project.mc_version + "-" + project.forge_version
	runDir = "eclipse"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// Replace mod_version and mc_version
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

test {
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
}
